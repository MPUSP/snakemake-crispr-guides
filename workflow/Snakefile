# load rules
# -----------------------------------------------------
include: "rules/common.smk"


# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"


# container definition (optional)
container: "oras://ghcr.io/mpusp/snakemake-crispr-guides:1.3.0"


# target rule
# -----------------------------------------------------
rule all:
    input:
        "results/module_logs/log.txt",


# workflow rules
# -----------------------------------------------------
rule get_genome:
    input:
        fasta=lambda wildcards: (
            config["get_genome"]["fasta"]
            if config["get_genome"]["database"] == "manual"
            else []
        ),
        gff=lambda wildcards: (
            config["get_genome"]["gff"]
            if config["get_genome"]["database"] == "manual"
            else []
        ),
    output:
        fasta="results/get_genome/genome.fasta",
        gff="results/get_genome/genome.gff",
        fai="results/get_genome/genome.fasta.fai",
    params:
        database=config["get_genome"]["database"],
        assembly=config["get_genome"]["assembly"],
        gff_source_types=config["get_genome"]["gff_source_type"],
    log:
        path="results/get_genome/log/get_genome.log",
    message:
        "parsing genome GFF and FASTA files"
    wrapper:
        "https://raw.githubusercontent.com/MPUSP/mpusp-snakemake-wrappers/refs/heads/main/get_genome"


rule bowtie_index:
    input:
        rules.get_genome.output.fasta,
    output:
        path=directory("results/bowtie_index"),
    conda:
        "envs/bowtie_index.yml"
    log:
        path="results/bowtie_index/log.txt",
    message:
        "build bowtie index"
    shell:
        "bowtie-build {input} {output.path}/index > {log.path} 2>&1"


rule bsgenome:
    input:
        fasta=rules.get_genome.output.fasta,
        gff=rules.get_genome.output.gff,
    output:
        bsgenome="results/bsgenome/bsgenome.RData",
        seqinfo="results/bsgenome/seqinfo.RData",
    conda:
        "envs/bsgenome.yml"
    log:
        path="results/bsgenome/log.txt",
    message:
        "create BSgenome"
    script:
        "scripts/create_bsgenome.R"


rule design_guides:
    input:
        fasta=rules.get_genome.output.fasta,
        gff=rules.get_genome.output.gff,
        bowtie_index=rules.bowtie_index.output.path,
        bsgenome=rules.bsgenome.output.bsgenome,
        seqinfo=rules.bsgenome.output.seqinfo,
    output:
        expand(
            "results/design_guides/guideRNAs_{guideset}.RData",
            guideset=config["design_guides"]["target_type"],
        ),
        list_tx="results/design_guides/list_tx.csv",
    params:
        config["design_guides"],
        max_cores=workflow.cores,
    conda:
        "envs/design_guides.yml"
    log:
        path="results/design_guides/log.txt",
    message:
        "design guide RNAs using R crisprDesign"
    script:
        "scripts/design_guides.R"


rule filter_guides:
    params:
        config["filter_guides"],
        max_cores=workflow.cores,
    input:
        guideset="results/design_guides/guideRNAs_{guideset}.RData",
        list_tx=rules.design_guides.output.list_tx,
    output:
        guideset_csv="results/filter_guides/guideRNAs_{guideset}.csv",
    conda:
        "envs/design_guides.yml"
    log:
        path="results/filter_guides/guideRNAs_{guideset}.log",
    message:
        "filter designed guide RNAs"
    script:
        "scripts/filter_guides.R"


rule report_html:
    input:
        expand(
            "results/filter_guides/guideRNAs_{guideset}.csv",
            guideset=config["design_guides"]["target_type"],
        ),
        list_tx=rules.design_guides.output.list_tx,
    params:
        config["report"],
    output:
        html="results/report/report.html",
    conda:
        "envs/report.yml"
    log:
        path="results/report/html_log.txt",
    message:
        "visualize guide RNA prediction using R"
    script:
        "notebooks/report.Rmd"


rule report_pdf:
    input:
        html=rules.report_html.output.html,
    output:
        pdf="results/report/report.pdf",
    conda:
        "envs/report.yml"
    log:
        path="results/report/pdf_log.txt",
    message:
        "convert HTML to PDF output"
    shell:
        "weasyprint -v {input.html} {output.pdf} &> {log.path}"


rule module_logs:
    input:
        rules.get_genome.log.path,
        rules.bowtie_index.log.path,
        rules.design_guides.log.path,
        rules.report_html.log.path,
        rules.report_pdf.log.path,
    conda:
        "envs/basic.yml"
    log:
        path="results/module_logs/log.txt",
    message:
        "combine all module log files to single log"
    shell:
        "cat {input} >> {log.path}"


rule versions:
    input:
        expand(
            wfpath("envs/{module}"),
            module=listdir(wfpath("envs")),
        ),
    output:
        path="results/versions/log_packages.txt",
    conda:
        "envs/basic.yml"
    log:
        path="results/versions/log_env.txt",
    message:
        "collect software versions from conda envs"
    shell:
        "conda env export > {log.path};"
        "cat {input} >> {output.path}"
